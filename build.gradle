import java.util.regex.Pattern

buildscript {
    ext.support_version = '23.2.1'
    ext.kotlin_version = '1.0.5-2'
    ext.espresso_version = '2.2.2'
    ext.test_runner = '0.5'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersionName {
    doLast {
        def gradlePropertiesFile = new File('gradle.properties')
        def regex = '^\\s*COMMON_VERSION_NAME=(\\d+.\\d+.\\d+)'
        def versionPattern = Pattern.compile(regex, Pattern.MULTILINE)
        def matcher = versionPattern.matcher(gradlePropertiesFile.text)
        if (matcher.find()) {
            def gradlePropertiesText = gradlePropertiesFile.text.replaceFirst(matcher.group(1), versionName)
            gradlePropertiesFile.text = gradlePropertiesText
        }
    }
}

task updateReadme {
    doLast {
        def readmeFile = new File('README.md')
        def versionPattern = Pattern.compile("\\d+.\\d+.\\d+")
        def readmeText = versionPattern.matcher(readmeFile.text).replaceAll(commonVersionName())
        readmeFile.text = readmeText
    }
}

int commonVersionCode() {
    return 1
}

String commonVersionName() {
    return COMMON_VERSION_NAME
}

int commonCompileSdkVersion() {
    return 23
}

int commonTargetSdkVersion() {
    return commonCompileSdkVersion()
}

int commonMinSdkVersion() {
    return 16
}

String commonBuildToolsVersion() {
    return '24.0.0'
}

boolean isCI() {
    return "true" == System.env.CI
}
